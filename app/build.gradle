plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "dev.unusedvariable.vlr"
        minSdk 23
        targetSdk 31
        versionCode 7
        versionName "v0.0.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystorePropertiesFile = rootProject.file('local.properties')
            if (keystorePropertiesFile.exists()) {
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            }
            storeFile = file("keystore/vlr-gg.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD") ?: keystoreProperties['store.password']
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") ?: keystoreProperties['key.password']
            keyAlias System.getenv("SIGNING_KEY_ALIAS") ?: keystoreProperties['key.alias']
        }
    }

    buildTypes {
        def localProperties = new Properties()
        def localPropertiesFile = rootProject.file('local.properties')
        if (localPropertiesFile.exists()) {
            localProperties.load(new FileInputStream(localPropertiesFile))
        }
        debug {
            minifyEnabled false
            archivesBaseName = "$defaultConfig.applicationId-$defaultConfig.versionCode($defaultConfig.versionName)"
            buildConfigField "String", "TOKEN", System.getenv("API_TOKEN") ?: localProperties['token']
        }
        release {
            buildConfigField "String", "TOKEN", System.getenv("API_TOKEN") ?: localProperties['token']
            minifyEnabled true
            archivesBaseName = "$defaultConfig.applicationId-$defaultConfig.versionCode($defaultConfig.versionName)"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled  = true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn",
                             "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                             "-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi",
                             "-Xopt-in=kotlin.time.ExperimentalTime",
                             "-Xopt-in=androidx.compose.animation.ExperimentalAnimationApi",
                             "-Xopt-in=androidx.compose.foundation.ExperimentalFoundationApi",
                             "-Xopt-in=androidx.compose.material3.ExperimentalMaterial3Api",
                             "-Xopt-in=com.google.accompanist.pager.ExperimentalPagerApi",
                             "-Xopt-in=androidx.compose.runtime.InternalComposeApi"]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        excludes += '/META-INF/{AL2.0,LGPL2.1}'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.compose.material3:material3:1.0.0-alpha06'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    implementation 'androidx.navigation:navigation-compose:2.5.0-alpha03'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0-alpha03"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.0-alpha03"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.0-alpha03'

    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation "androidx.glance:glance-appwidget:1.0.0-alpha03"

    implementation 'androidx.core:core-splashscreen:1.0.0-beta01'


    def accompanist = "0.24.3-alpha"
    implementation "com.google.accompanist:accompanist-insets:$accompanist"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist"
    implementation "com.google.accompanist:accompanist-placeholder-material:$accompanist"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist"
    implementation "com.google.accompanist:accompanist-pager:$accompanist"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist"

    implementation "androidx.browser:browser:1.4.0"

    implementation "io.coil-kt:coil-compose:1.4.0"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-common:1.0.0"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Room
    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2"

    def ktor_version = "1.6.7"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"
    implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-encoding-jvm:$ktor_version"

    implementation "com.github.skydoves:landscapist-glide:1.4.8"

    implementation "com.dropbox.mobile.store:store4:4.0.4-KT15"

    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")
}